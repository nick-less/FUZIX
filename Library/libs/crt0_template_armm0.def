.code 16
.text
.thumb_func
.align 4
.globl _start
_start:
	# Wipe BSS.
	# FIXME: should have been done by execve so this ought to be
	# removable

	ldr r0, =__bss_start
	ldr r1, =__bss_end
	mov r2, #0
.1:
	cmp r0, r1
	bge .2
	str r2, [r0]
	add r0, #4
	b .1
.2:

#if defined WITH_STDIO
	bl __stdio_init_vars
#endif

	/* Fetch argc and argv from the stack and set up the environment pointer. */

	ldr r0, [sp, #0] /* argc */
	ldr r1, [sp, #4] /* argv */
	add r2, sp, #8   /* envp */

	/* Store envp. */

	ldr r3, =environ
	str r2, [r3]

	/* Call main, and then exit. */

	bl main
	bl exit

.globl environ
.comm environ, 4

.text
.global __sighandler
__sighandler:
	swi 0

