;
;	We have 0x88 bytes before the standard UZI start point, everyone
;	breathe in
;	On entry h is set to 0 and the top of screen is banked at 0x8000
;
track		.equ 0xF1EA
		.org 0

progress:	; top bar
		; bank then down 14 character lines
		; and 
		ld de, 0x8000+0x2760+312
		ld a, font0 % 256
		rst 0x18	; choline
		; progress bar
		ld de, 0x8000+0x2760+312+720
		rst 0x18	; choline
		ld de, 0x8000+0x2760+312+720+8
		ld a, (track)
		inc a
		ld b, a
		ld a, fontX
		rst 0x28	; nchout
		; bottom bar
		ld de, 0x8000+0x2760+312+2*720
choline:	rst 0x20	; chout	at 0x18
		add a,8
		ld b, 10
		rst 0x28	; nchout
		jr tail
chout:		ld l, a		; chout at 0x20
		ld bc, 8
		ldir
		ret
		nop		; and nchout at 0x28
nchout:		push bc
		rst 0x20
		pop bc
		djnz nchout
tail2:		add a, 8
		ret
tail:		rst 0x20
		jr tail2

font0:		db	0x00, 0x00, 0x1f, 0x1f, 0x1f, 0x1f, 0x1f, 0x1f
font1:		db	0x00, 0x00, 0xff, 0xff, 0xff, 0xff, 0xff, 0xff
font2:		db 	0x00, 0x00, 0xf8, 0xf8, 0xf8, 0xf8, 0xf8, 0xf8

font3:		db	0x1f, 0x1f, 0x1f, 0x1f, 0x1f, 0x1f, 0x1f, 0x1f
font4:		db	0x00, 0x00, 0xff, 0xff, 0xff, 0xff, 0x00, 0x00
font5:		db	0xf8, 0xf8, 0xf8, 0xf8, 0xf8, 0xf8, 0xf8, 0xf8

fontX:		db	0x00, 0x00, 0x01, 0x01, 0x01, 0x01, 0x00, 0x00

font6:		db	0x1f, 0x1f, 0x1f, 0x1f, 0x1f, 0x1f, 0x00, 0x00
font7:		db	0xff, 0xff, 0xff, 0xff, 0xff, 0xff, 0x00, 0x00
font8:		db 	0xf8, 0xf8, 0xf8, 0xf8, 0xf8, 0xf8, 0x00, 0x00

		; Pad to 0x88 bytes
		db	0,0,0,0,0
